;
	PUBLIC	SAVE
;
LEV	EQU	006
SHIR	EQU	020
WERH	EQU	0FFH
WISOTA	EQU	080H
;
MAXTREK 	EQU	165
;
PRINT	EQU	0F809H
;
RUS	EQU	004H
PORTA	EQU	007H
PORTB	EQU	006H
PORTC	EQU	005H
;
BELYJ	EQU	377O
SSERYJ	EQU	122O
TSERYJ	EQU	244O
TEMNYJ	EQU	100O
;
;
SAVE:	MVI	A,10011000B
	OUT	RUS
;
	XRA	A
	OUT	010H
;
	LXI	H,OPROS
	SHLD	00039H
	EI
	HLT
;
	MVI	A,0
	STA	@TREK
	CALL	@GOTR2
;
LOOPSMOTR:
	CALL	KADR1
	CALL	KADR2
	LXI	H,8B80H
	REPT	5
		CALL	KADR3
	ENDM
;
	IN	001H
	ANI	080H
	JNZ	LOOPSMOTR
;
	CALL	KADR1
;
LOOPWRITE:
	CALL	@GOTR
	CALL	KADR2
	LXI	H,08B80H
;
	IRP	SEC,<1,2,3,4,5>
		PUSH	H
			CALL	KADR3
		POP	H
		IF	SEC=5
			CALL	KADR1
		ENDIF
		LDA	@USKNGMD
		OUT	01CH
		MVI	A,SEC
		CALL	SECWRITE
		IN	01BH
		ANA	A
		CNZ	PIPI
		LDA	@USKNGMD
		OUT	01CH
	ENDM
;
	IN	001H
	ANI	040H
	JZ	EXIT
;
	LDA	@TREK
	INR	A
	STA	@TREK
	CPI	MAXTREK
	JC	LOOPWRITE
	JMP	EXIT
;
;
SECWRITE:
	OUT	019H	; Установить номер сектора
	MVI	A,0A0H	; Запись сектора
	OUT	01BH
	LXI	D,02080H
	MVI	C,002H
	CALL	@PGMD
;
	REPT	5
	LOCAL	M1,M2,M3,M4
M1:
		REPT	4
			IN	01BH
			ANA	C
			JNZ	M2
		ENDM
;
		IN	01BH
		RRC
		RNC
		RRC
		JNC	M1
M2:
		MOV	A,M
		OUT	018H
		INR	L
		JNZ	M1
;
		MVI	D,020H
		DAD	D
;
M3:
		REPT	4
			IN	01BH
			ANA	C
			JNZ	M4
		ENDM
;
		IN	01BH
		RRC
		RNC
		RRC
		JNC	M3
M4:
		MOV	A,M
		OUT	018H
		INR	L
		JNZ	M3
;
		MVI	D,0E1H
		DAD	D
	ENDM
;
	RET
;
;
KADR1:
	XRA	A
	OUT	PORTC
	MVI	A,008H
	OUT	PORTC	; Старт
	MVI	A,007H
	OUT	000H	; Включить
	RET
;
KADR2:
LOOPWAIT:
	IN	PORTC
	RLC
	JC	LOOPWAIT
;
	MVI	A,006H
	OUT	000H	; Выключить
;
	RET
;
KADR3:
	MVI	C,004H
LOOPKADR:
	MVI	A,00AH
	OUT	PORTC	; Старший адрес
	MOV	A,H
	OUT	PORTB	; Зафиксирован
	MVI	A,009H
	OUT	PORTC	; Ждем младший адрес
;
LOOPKADR1:
	REPT	080H
	MOV	A,L
	OUT	PORTB
	IN	PORTA
	MOV	M,A
	INR	L
	ENDM
;	JNZ	LOOPKADR1
;
	MVI	L,080H
	MVI	A,020H
	ADD	H
	MOV	H,A
;
	MVI	A,00AH
	OUT	PORTC	; Старший адрес
	MOV	A,H
	OUT	PORTB	; Зафиксирован
	MVI	A,009H
	OUT	PORTC	; Ждем младший адрес
;
LOOPKADR2:
	REPT	080H
	MOV	A,L
	OUT	PORTB
	IN	PORTA
	MOV	M,A
	INR	L
	ENDM
;	JNZ	LOOPKADR2
;
	MVI	L,080H
	MVI	A,0E1H
	ADD	H
	MOV	H,A
;
	DCR	C
	JNZ	LOOPKADR
	RET
;
;
EXIT:
	JMP	00000H
;
;
OPROS:
	PUSH	H
	PUSH	D
	PUSH	PSW
	LXI	D,0100FH
M1:	LXI	H,PALITRA1
;
CWET1:		; строка = 48 машинных циклов
	MOV	A,E
	OUT	002H
	MOV	A,M
	OUT	00CH
	XTHL
	OUT	00CH
	XTHL
	OUT	00CH
	DCX	H
	DCR	E
	DCR	D
	OUT	00CH
	JNZ	CWET1
;
	XRA	A
	OUT	002H
	MVI	A,7
	OUT	003H
;
	POP	PSW
	POP	D
	POP	H
;
	RET
;
;
	DB	TEMNYJ
	DB	TEMNYJ
	DB	TEMNYJ
	DB	TEMNYJ
	DB	TSERYJ
	DB	TSERYJ
	DB	TSERYJ
	DB	TSERYJ
	DB	SSERYJ
	DB	SSERYJ
	DB	SSERYJ
	DB	SSERYJ
	DB	BELYJ
	DB	BELYJ
	DB	BELYJ
PALITRA1:	DB	BELYJ
;
	DB	TEMNYJ
	DB	TSERYJ
	DB	SSERYJ
	DB	BELYJ
	DB	TEMNYJ
	DB	TSERYJ
	DB	SSERYJ
	DB	BELYJ
	DB	TEMNYJ
	DB	TSERYJ
	DB	SSERYJ
	DB	BELYJ
	DB	TEMNYJ
	DB	TSERYJ
	DB	SSERYJ
PALITRA2:	DB	BELYJ
;
;
MOVBLK:
	MOV	A,M
	STAX	D
	INX	H
	INX	D
	DCX	B
	MOV	A,B
	ORA	C
	JNZ	MOVBLK
	RET
;
;
;
EXITZ:
	DI
	MVI	A,023H
	OUT	010H
;
	CALL	0F800H
; Запись в файл пакованной графики
;
	JMP	00000H
;
;
; Перемещение головки на нужный трек
@GOTR:
	CALL	@MAKEUS
AE7E0:		; Разгон двигателя
	IN	01BH
	RLC
	JNC	GOTOW
;	LDA	@USKNGMD
	MOV	A,B
	OUT	01CH
;
	LXI	H,0000H
AE7E8:
	DCX	H
	MOV	A,H
	ORA	L
	JNZ	AE7E8
	JMP	AE7E0
;
GOTOW:
	MOV	A,B
	OUT	01CH
	LXI	H,@TE954	; Таблица текущих треков НГМД
	LDA	@DISK
	ANI	001H
	JZ	TTTR
	INX	H
TTTR:
	MOV	A,M
;	ANA	A
	RAR
	OUT	01AH
;
	LDA	@TREK
	MOV	M,A
	ANA	A
	RAR
;
@GOTR3:
	DI
	OUT	018H ; Установить номер дорожки
	MVI	A,010H
	OUT	01BH ; Задать режим поиска
	CALL	@PGMD
DOR1:
;	LDA	USKNGMD
	MOV	A,B
	OUT	01CH
	IN	01BH ; Проверить конец выполнения поиска
	RRC
	JC	DOR1
;
	LDA	@USKNGMD
;	MOV	A,B
	OUT	01CH
;
@PGMD:
	MVI	A,060H
@AE7F7:
	DCR	A
	JNZ	@AE7F7
;
	RET
;
;
;
@USKNGMD:	DB	000H
@WG93:		DB	000H
@DISK:		DB	000H
@TREK:		DB	000H
@SEC:		DB	001H
@PDP:		DW	00000H
@TE954:
	DB	008H
	DB	008H
	DB	008H
	DB	008H
;
; Формирование управляющего слова
; контроллера НГМД
@MAKEUS:
	LDA	@DISK
	ANI	003H
	MOV	C,A
	LDA	@TREK
	RRC
	MVI	A,004H
	JNC	AE7D9
	XRA	A
AE7D9:
	ORA	C
	MOV	B,A
	ORI	030H
;	MOV	B,A
	STA	@USKNGMD
	RET
;
;
;	Выход из п/п чтения/записи
@DEXIT:
;	LDA	USKNGMD
	MOV	A,B
	OUT	01CH
	EI
	IN	01BH
	STA	@WG93
	ANA	A
	RZ
AE795:
	ANI	010H	; Не найден
	CNZ	@GOTR2
;
AE79A:
	LDA	@WG93
	ANA	A
	RET
;
;
;
; Повторный поиск дорожки
@GOTR2:
	CALL	@MAKEUS
	XRA	A
	OUT	01BH
	CALL	@PGMD
;
GTR1:
;	LDA	USKNGMD
	MOV	A,B
	OUT	01CH
	IN	01BH ; Проверить конец восстановления
	RRC
	JC	GTR1
;
	CALL	@PGMD
	LDA	@TREK
	ANA	A
	RAR
	JMP	@GOTR3
;
;
PIPI:
	XRA	A
	MVI	C,020H
PIPI2:
	MVI	B,020H
	XRI	001H
	OUT	000H
PIPI3:
	DCR	B
	JNZ	PIPI3
	DCR	C
	JNZ	PIPI2
	RET
;
;
	END
