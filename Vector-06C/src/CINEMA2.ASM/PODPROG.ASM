;
	PUBLIC	NCUR1, NCUR2, STRCUR, CURBOX
	PUBLIC	PAUSE, KEYJOY, KODKEY, PALIT0
	PUBLIC	PRER1, INKEY, PALIT1, PALIT2
	PUBLIC	GET, PUT, INNAME, LPEN, SPEN
	PUBLIC	QUEST
;
	EXTRN	CURSYX, PEN, CLSBOX, RETURN
	EXTRN	PURGE, @TREK, @GOTR
;
	EXTRN	@PLTR
	EXTRN	@WRITE, @DRVON, @DRVOF
	EXTRN	@CONIN, @KBD, @SCUR, @SCRL
	EXTRN	@ZG, @PRINT
	EXTRN	@IDOS, @EDOS
;
;==== ПАУЗА ====
PAUSE:	HLT
	DCR	A
	JNZ	PAUSE
	RET
;==== КУРСОР В КВАДРАТЕ? ====
; BC - начало
; DE - конец
CURBOX: MOV	A,C
	STA	BOXZ1+1
	MOV	A,E
	STA	BOXZ2+1
	MOV	A,B
	STA	BOXZ3+1
	MOV	A,D
	STA	BOXZ4+1
	LHLD	CURSYX
;
BOXZ1:	MVI	A,0
	CMP	L
	JZ	BOX1
	JNC	BOX3
BOX1:
BOXZ2:	MVI	A,0
	CMP	L
	JC	BOX3

BOXZ3:	MVI	A,0
	CMP	H
	JZ	BOX2
	JNC	BOX3
BOX2:
BOXZ4:	MVI	A,0
	CMP	H
	MVI	A,0FFH
	RNC
BOX3:	XRA	A
	RET
;====
; нарисовать курсор
NCUR2:	LXI	B,GCUR2-48
	JMP	NARCUR
NCUR1:	LXI	B,GCUR1-48
; BC - адрес спрайта-48
NARCUR: MOV	E,C
	MOV	D,B
	LHLD	CURSYX
	XCHG
	MVI	A,00000111B
	ANA	D
	LXI	B,48
NARCUR1:
	DAD	B
	SUI	2
	JP	NARCUR1
;
	MVI	A,11111000B
	ANA	D
	RRC
	RRC
	RRC
	ADI	080H
	MOV	D,A
; HL - откуда
; DE - куда
; Собственно вывод
	SHLD	NARCUR2+1
	LXI	B,CURBAK
NARCUR2:
	LXI	H,GCUR1
	REPT 12
		LDAX	D
		STAX	B
		INX	B
		ANA	M
		INX	H
		ORA	M
		INX	H
		STAX	D
		DCR	E
	ENDM
	INR	D
	REPT 12
		INR	E
		LDAX	D
		STAX	B
		INX	B
		ANA	M
		INX	H
		ORA	M
		INX	H
		STAX	D
	ENDM
	MVI	A,01FH
	ADD	D
	MOV	D,A
	JM	NARCUR2
	RET
;----
; нарисовать подложку
STRCUR: LXI	D,CURBAK
	LHLD	CURSYX
	MVI	A,11111000B
	ANA	H
	RRC
	RRC
	RRC
	ADI	080H
STRCUR1:
	MOV	H,A
	REPT 12
		LDAX	D
		MOV	M,A
		INX	D
		DCR	L
	ENDM
	INR	H
	RZ
	REPT  12
		INR	L
		LDAX	D
		MOV	M,A
		INX	D
	ENDM
	MVI	A,01FH
	ADD	H
	JM	STRCUR1
	RET
;----
CURBAK: DS	24*4
;
GCUR1:	DB	127,0,63,0,95,64,111,96,119,112,123,120
	DB	125,124,113,112,87,80,43,8,107,8,247,0
	DB	255,0,255,0,255,0,255,0,255,0,255,0
	DB	255,0,255,0,255,0,255,0,255,0,255,0
	DB	223,0,207,0,215,16,219,24,221,28,222,30
	DB	223,31,220,28,213,20,202,2,218,2,253,0
	DB	255,0,255,0,255,0,255,0,127,0,127,0
	DB	255,0,255,0,255,0,255,0,255,0,255,0
	DB	247,0,243,0,245,4,246,6,247,7,247,7
	DB	247,7,247,7,245,5,242,0,246,0,255,0
	DB	127,0,191,128,191,128,127,0,31,0,223,192
	DB	191,128,127,0,255,0,255,0,255,0,255,0
	DB	253,0,252,0,253,1,253,1,253,1,253,1
	DB	253,1,253,1,253,1,252,0,253,0,255,0
	DB	223,0,175,32,175,32,95,64,199,192,247,240
	DB	239,224,223,192,191,128,127,0,255,0,255,0
;
GCUR2:	DB	1,0,125,124,69,68,85,84,171,40,215,16
	DB	215,16,171,40,85,84,109,108,125,124,1,0
	DB	255,0,255,0,255,0,255,0,255,0,255,0
	DB	255,0,255,0,255,0,255,0,255,0,255,0
	DB	192,0,223,31,209,17,213,21,234,10,245,4
	DB	245,4,234,10,213,21,219,27,223,31,192,0
	DB	127,0,127,0,127,0,127,0,255,0,255,0
	DB	255,0,255,0,127,0,127,0,127,0,127,0
	DB	240,0,247,7,244,4,245,5,250,2,253,1
	DB	253,1,250,2,245,5,246,6,247,7,240,0
	DB	31,0,223,192,223,192,95,64,191,128,127,0
	DB	127,0,191,128,95,64,95,64,223,192,31,0
	DB	252,0,253,1,253,1,253,1,254,0,255,0
	DB	255,0,254,0,253,1,253,1,253,1,252,0
	DB	7,0,247,240,183,176,87,80,175,160,95,64
	DB	95,64,175,160,87,80,23,16,247,240,7,0
;
;==== ПРЕРЫВАНИЯ ====
PRER1:	MVI	A,10001010B
	OUT	000H
;
	MVI	A,0FEH
	OUT	003H
	IN	002H
	STA	KODKUR
;
	MVI	A,07FH
	OUT	003H
	IN	002H
	STA	KODPRO
;
	MVI	A,088H
	OUT	0
	MVI	A,5
	OUT	2
	MVI	A,7
	OUT	3
	EI
	RET
;---- ЗАЩИТА ПРОТИВ ДВОЙНОГО НАЖАТИЯ ----
INKEY:	CALL	INK0
INKZ0:	LXI	H,KODBAK
	CMP	M
	MOV	M,A
	JZ	INKZ1
	CALL	INK0
	CMP	M
	RNZ
	STA	KODKEY
	MVI	A,10
	STA	SCET
	RET
;
INKZ1:	LXI	H,KODKEY
	MVI	M,0FFH
	LXI	H,SCET
	DCR	M
	RNZ
	MVI	M,001H
	STA	KODKEY
	RET
;---- ФОРМИРОВАНИЕ БАЙТА ----
INK0:	LDA	KODKUR
	MOV	B,A
	LDA	KODPRO; 	ПРОБЕЛ
	ANI	080H
	JNZ	INK1
	MOV	A,B
	ANI	11111011B
	MOV	B,A
INK1:	MOV	A,B
KEYJOY: RET
;				ОПРОС ДЖОЙСТИКА
JOYN:	IN	006H
	MOV	C,A
	ANI	008H
	JNZ	JOYV
	MOV	A,B
	ANI	01111111B
	MOV	B,A
JOYV:
	MOV	A,C
	ANI	004H
	JNZ	JOYL
	MOV	A,B
	ANI	11011111B
	MOV	B,A
JOYL:
	MOV	A,C
	ANI	002H
	JNZ	JOYP
	MOV	A,B
	ANI	11101111B
	MOV	B,A
JOYP:
	MOV	A,C
	ANI	001H
	JNZ	JOYKN1
	MOV	A,B
	ANI	10111111B
	MOV	B,A
JOYKN1:
	MOV	A,C
	ANI	080H
	MOV	A,B
	RNZ
	ANI	11111011B
	RET
;----
KODKEY: DB	0FFH
KODBAK: DB	0FFH
KODPRO: DB	0FFH
KODKUR: DB	0FFH
SCET:	DB	10
;
PALIT0: DB	0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
PALIT1: DB	64,64,64,64,164,164,164,164,82,82,82,82,246,246,246,246
PALIT2: DB	64,164,82,246,64,164,82,246,64,164,82,246,64,164,82,246
;
;==== ВЫВОД PENа ====
PUT:	CALL	GENER
	MVI	A,01010101B
	STA	PUT2+1
	LXI	H,PEN
	MOV	A,M
	STA	PUT1+1
	INX	H
	MOV	C,M
	INX	H
	XCHG
	MVI	L,080H
PUT1:	MVI	B,0
	MVI	H,058H
PUT2:	MVI	A,01010101B
	RRC
	STA	PUT2+1
	LDAX	D
	JC	PUT3
	INX	D
	ANI	00000011B
	JZ	NOPUT2
	RLC
	RLC
	RLC
	JMP	PUT4
PUT3:	ANI	00110000B
	JZ	NOPUT2
	RRC
PUT4:	PUSH	B
		LXI	B,PLOT1-8
		ADD	C
		MOV	C,A
		MVI	A,0
		ADC	B
		MOV	B,A
		MVI	A,057H
		CMP	H
		JNC	NOPUT1
		MVI	A,0F7H
		CMP	H
		JC	NOPUT1
		PUSH	D
			PUSH	H
				CALL	PLOT
			POP	H
		POP	D
NOPUT1: POP	B
NOPUT2: INR	H
	DCR	B
	JNZ	PUT2
	INR	L
	RZ
	DCR	C
	JNZ	PUT1
	RET
;==== ЗАПОМИНАНИЕ PENа ====
GET:	CALL	GENER
	LXI	H,PEN
	MVI	M,0A0H
	INX	H
	MVI	M,080H
	INX	H
	MVI	C,080H
	MVI	D,01010101B
GET1:	MVI	B,058H
GET2:	PUSH	B
		PUSH	H
			CALL	PGET
			MOV	E,A
		POP	H
	POP	B
	MOV	A,D
	RRC
	MOV	D,A
	MOV	A,E
	JC	GET3
	ORA	M
	MOV	M,A
	INX	H
	JMP	GET4
GET3:	RLC
	RLC
	RLC
	RLC
	MOV	M,A
GET4:	INR	B
	MVI	A,0F8H
	CMP	B
	JNZ	GET2
	INR	C
	JNZ	GET1
	RET
;====
; генерация матрицы точек
GENER:	LXI	H,07E00H
	MVI	A,080H
	MOV	C,A
GENMAT: MOV	M,C
	INR	H
	MOV	M,A
	RRC
	INR	L
	RZ
	DCR	H
	JNC	GENMAT
	INR	C
	JMP	GENMAT
;====
; вход: HL - X,Y
;	BC - адрес подпрограммы вывода цвета
PLOT:	PUSH	B
		MOV	E,H
		MVI	D,07EH
		LDAX	D
		MOV	H,A
		INR	D
		LDAX	D
		MOV	D,A
		CMA
		MOV	E,A
		LXI	B,02000H
		RET
;
PLOT1:	MOV	A,M
	ANA	E
	MOV	M,A
	DAD	B
	MOV	A,M
	ORA	D
	MOV	M,A
	RET
;
PLOT2:	MOV	A,M
	ORA	D
	MOV	M,A
	DAD	B
	MOV	A,M
	ANA	E
	MOV	M,A
	RET
;
PLOT3:	MOV	A,M
	ORA	D
	MOV	M,A
	DAD	B
	MOV	A,M
	ORA	D
	MOV	M,A
	RET
;====
; определить цвет точки
; вход:  BC - X,Y
; выход: A - цвет
PGET:	MOV	L,B
	MVI	H,07EH
	MOV	B,M
	INR	H
	MOV	L,M
;
	LDAX	B
	ANA	L
	SUB	L
	RAL
	MOV	H,A
;
	MVI	A,020H
	ADD	B
	MOV	B,A
;
	LDAX	B
	ANA	L
	SUB	L
	MOV	A,H
	RAL
;
	CMA
	ANI	00000011B
	RET
;
;==== ВВОД ИМЕНИ ФАЙЛА ====
NCURY:	EQU	20
NCURX:	EQU	15
;
INNAME: CALL	IKBD
	LXI	D,@ZG+155*8
	CALL	@SCUR
; вниз
WNIZ:	LXI	D,BUFNAM
	MVI	L,NCURY
	MVI	H,NCURX-1
ENDNAM: LDAX	D
	INX	D
	INR	H
	ANA	A
	JNZ	ENDNAM
	SHLD	CURY
;
WRTNAM: LXI	D,PRNNAM
	CALL	@WRITE
	MVI	C,' '
	CALL	@PRINT
	LXI	D,PRNCUR
	CALL	@WRITE
;---- ВВОД ИМЕНИ ----
INNAM0: CALL	@CONIN
	ANI	01111111B
	CPI	00DH
	JZ	ENTER
	CPI	01BH
	JZ	ESC
	CPI	01AH
	JZ	WNIZ
; ввод USERа
	MOV	C,A
	LDA	CURY
	CPI	NCURY
	MOV	A,C
	JZ	NOUSER
	CPI	'!'
	JC	INNAM0
	CPI	'0'
	JC	INNAM0
	CPI	':'
	JC	SETUSR
	CPI	'A'
	JC	INNAM0
	CPI	'G'
	JNC	INNAM0
SETUSR: STA	BUFUSR
	JMP	WRTNAM
;
NOUSER: CPI	001H
	JZ	F2
	CPI	07FH
	JZ	DEL
	CPI	019H
	JZ	WWERH
	CPI	008H
	JZ	WLEWO
	CPI	018H
	JZ	WPRAWO
; символ
	CPI	'!'
	JC	INNAM0
	MOV	B,A
	LXI	H,CURX
	MVI	A,NCURX+13
	SUB	M
	JM	INNAM0
	LXI	H,BUFNAM+13
	JZ	SYMEND
	LXI	D,BUFNAM+12
	MOV	C,A
RAZDW:	LDAX	D
	MOV	M,A
	DCX	D
	DCX	H
	DCR	C
	JNZ	RAZDW
SYMEND: MOV	M,B
	JMP	WPRAWO
;----
; ВК
ENTER:	JMP	EKBD
; АР2
ESC:	CALL	EKBD
	JMP	RETURN
; вверх
WWERH:	MVI	L,NCURY-1
	MVI	H,NCURX+9
	SHLD	CURY
	JMP	WRTNAM
; влово
WLEWO:	LXI	H,CURX
	MVI	A,NCURX
	CMP	M
	JZ	INNAM0
	DCR	M
	JMP	WRTNAM
; вправо
WPRAWO: LDA	CURX
	MOV	E,A
	MVI	D,0
	LXI	H,BUFNAM-NCURX
	DAD	D
	XRA	A
	CMP	M
	JZ	INNAM0
	LXI	H,CURX
	INR	M
	JMP	WRTNAM
; ЗБ
DEL:	LXI	H,CURX
	MVI	A,NCURX
	CMP	M
	JZ	INNAM0
	DCR	M
; F2
F2:	LDA	CURX
	SUI	NCURX
	MOV	E,A
	MVI	D,0
	LXI	H,BUFNAM
	DAD	D
	MOV	E,L
	MOV	D,H
	INX	D
SOMKN:	LDAX	D
	MOV	M,A
	INX	D
	INX	H
	ANA	A
	JNZ	SOMKN
	JMP	WRTNAM
;----
PRNNAM: DB	01BH,NCURY-1,NCURX+4,'User '
BUFUSR: DB	'0',01BH,NCURY,NCURX
BUFNAM: DB	'C:NONAME.PEN',0,0,0,0
PRNCUR: DB	01BH
CURY:	DB	NCURY
CURX:	DB	NCURX,0
;
;==== ЗАГРУЗКА PENа ====
LPEN:	CALL	@IDOS
	CALL	SETPRM
	CALL	FILL0
	CALL	QUEST
	CALL	QUEST
	JZ	NOFILE
;
	XRA	A
	STA	EXDSC
	LXI	H,PEN
	SHLD	EXDSC+3
	LXI	H,08000H
	SHLD	EXDSC+5
	CALL	FILL0
;
	MVI	C,032H
	LXI	D,BIOSBP
	CALL	00005H
;
	JMP	BEGTRK
;==== ВЫГРУЗКА PENа ====
SPEN:	CALL	@IDOS
	CALL	SETPRM
	CALL	FILL0
	CALL	QUEST
	CALL	QUEST
	CNZ	ZAMENA
; Удаление файла
	LXI	D,0005CH
	MVI	C,013H
	CALL	00005H
; Создание файла
	LXI	D,0005CH
	MVI	C,16H
	CALL	00005H
;
	MVI	A,0FFH
	STA	EXDSC
	LXI	H,PEN
	SHLD	EXDSC+3
	LXI	H,PEN+10242
	SHLD	EXDSC+5
	CALL	FILL0
;
	LXI	D,BIOSBP
	MVI	C,032H
	CALL	00005H
; Закрытие файла
	LXI	D,0005CH
	MVI	C,010H
	CALL	00005H
;----
BEGTRK: CALL	@EDOS
BTREK:	LDA	@TREK
	PUSH	PSW
		XRA	A
		STA	@TREK
		CALL	@GOTR
	POP	PSW
	STA	@TREK
	EI
	RET
;----
; Поиск первого входа
QUEST:	MVI	C,011H
	LXI	D,0005CH
	CALL	00005H
	INR	A
	RET
;----
NOFILE: CALL	BEGTRK
	CALL	PURGE
	LXI	D,MNOFIL
	CALL	@WRITE
	MVI	A,100
	CALL	PAUSE
	JMP	RETURN
;----
ZAMENA: CALL	@EDOS
	CALL	PURGE
	CALL	IKBD
	LXI	D,MZAMEN
	CALL	@WRITE
ZAMLOOP:
	CALL	@CONIN
	ANI	01111111B
	CPI	'D'
	JZ	YESZAM
	CPI	'd'
	JZ	YESZAM
	CPI	'N'
	JZ	NOZAM
	CPI	'n'
	JZ	NOZAM
	CPI	01BH	; АР2
	JNZ	ZAMLOOP
NOZAM:	CALL	BTREK
	JMP	ESC
;
YESZAM: CALL	EKBD
	CALL	PURGE
	JMP	@IDOS
;----
NOFORM: CALL	BEGTRK
	CALL	PURGE
	LXI	D,MNOFRM
	CALL	@WRITE
	MVI	A,100
	CALL	PAUSE
	JMP	RETURN
;----
IKBD:	MVI	A,7
	STA	@SCRL
	XRA	A
	LXI	D,@KBD
	JMP	@DRVON
;----
EKBD:	LXI	D,@ZG+32*8
	CALL	@SCUR
	LXI	D,@KBD
	CALL	@DRVOF
	MVI	A,0FFH
	STA	@SCRL
	RET
;----
SETPRM: MVI	C,098H	; установка имени
	LXI	D,PFCB
	CALL	00005H
; только PEN
	LXI	H,0005CH+9
	MVI	A,'P'
	CMP	M
	JNZ	NOFORM
	INX	H
	MVI	A,'E'
	CMP	M
	JNZ	NOFORM
	INX	H
	MVI	A,'N'
	CMP	M
	JNZ	NOFORM
; установка USERа
	MVI	C,020H
	LDA	BUFUSR
	SUI	'0'
	CPI	00AH
	MOV	E,A
	JC	00005H
	SUI	7
	MOV	E,A
	JMP	00005H
;
PFCB:	DW	BUFNAM
	DW	0005CH
;----
FILL0:	LXI	H,0005CH+12
	MVI	A,22
LOOPFIL0:
	MVI	M,0
	INX	H
	DCR	A
	JNZ	LOOPFIL0
	RET
;----
BIOSBP: DB	129	; Прямой обмен с файлом
	DW	EXDSC	; Дескриптор операции
EXDSC:	DB	000H	; Чтение
	DW	0005CH
	DW	PEN
	DW	08000H
;----
MNOFIL: DB	01BH,18,18,'Нет файла',0
;
MNOFRM: DB	01BH,18,17,  'Формат  не'
	DB	01BH,19,15,'обрабатывается',0
;
MZAMEN: DB	01BH,19,16, 'Файл уже есть'
	DB	01BH,20,15,'Заменять (Д/Н)?',0
;
	END
