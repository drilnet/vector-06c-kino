;
	PUBLIC	CURSYX, REVERS, RETURN
	PUBLIC	PKADR, KKADR, PRNTK
	PUBLIC	ERRORR, ERRORS, ERRORD
	PUBLIC	PURGE, PRNMNU, CLSBOX, LOOP1
;
	EXTRN	INKEY, KODKEY, KEYJOY, PAUSE
	EXTRN	SPR, PUT, INNAME, LPEN
	EXTRN	NCUR1, NCUR2, STRCUR, CURBOX
	EXTRN	PALIT0, PRER1, PALIT1, PALIT2
	EXTRN	SPEN, GET, QUEST
	EXTRN	@TREK, @GOTR, @DISK, LIST
	EXTRN	READ, VIEW, RECORD, RECPEN
;
	EXTRN	@INIT, @EXIT, @DRVOF, @DRVON
	EXTRN	@SCOL, @PLTR, @PLTRS, @BRDC
	EXTRN	@DECA, @WRITE, @DESPR, @CLS
	EXTRN	@CLRS
;
;==== НАЧАЛО ====
	JMP	START
	DB	' CINEMA версия от 22.08.1998г. Совместное производство'
	DB	' Третьякова А. Л. и Терентьева С. В. '
START:	MVI	C,020H
	MVI	E,0FFH
	CALL	00005H
	STA	NUSER+1
	LXI	D,START
	CALL	@INIT
	XRA	A
	LXI	D,PRER1
	CALL	@DRVON
	LXI	D,PALIT0
	CALL	@PLTR
	LXI	H,RST0
	SHLD	00001H
	MVI	A,130
	OUT	4
	IN	6
	INR	A
	JNZ	NOJOY
	STA	KEYJOY
NOJOY:	LXI	D,SPR
	CALL	@DESPR
	LXI	B,08000H
	LXI	D,0C000H
	LXI	H,04000H
PERSCR: LDAX	B
	STAX	D
	INX	B
	INX	D
	DCX	H
	MOV	A,H
	ORA	L
	JNZ	PERSCR
;
	MVI	A,05AH
	CALL	@SCOL
	CALL	PRNMNU
	CALL	PRNMP
	CALL	PRNTK
	CALL	PRNPK
	CALL	PRNKK
	CALL	PRNDSK
	CALL	PRNREV
	MVI	A,5
	STA	@BRDC
	LXI	D,PALIT1
	CALL	@PLTRS
;
;==== ОСНОВНОЙ ЦИКЛ ====
LOOP1:	CALL	PERCUR
	LXI	H,OKNOYX
OKNOLOOP:
	MOV	C,M
	MOV	A,C
	INX	H
	MOV	B,M
	ORA	B
	JZ	LOOP1
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	PUSH	H
		CALL	CURBOX
	POP	H
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	ANA	A
	JZ	OKNOLOOP
	XCHG
	PCHL
;----
OKNOYX: DW	00CDCH
	DW	01BECH
	DW	KADRTM
;
	DW	03CDCH
	DW	04BECH
	DW	KADRTP
;
	DW	00CB4H
	DW	01BC3H
	DW	KADRPM
;
	DW	03CB4H
	DW	04BC4H
	DW	KADRPP
;
	DW	00C8CH
	DW	01B9BH
	DW	KADRKM
;
	DW	03C8CH
	DW	04B9BH
	DW	KADRKP
;
	DW	00C6CH
	DW	01B7BH
	DW	DISKA
;
	DW	03C6CH
	DW	04B7BH
	DW	DISKB
;
	DW	00C44H
	DW	02B53H
	DW	REVON
;
	DW	03444H
	DW	05B53H
	DW	REVOFF
;
	DW	00424H
	DW	07B33H
	DW	KLOAD
;
	DW	0040CH
	DW	07B1BH
	DW	KSAVE
;
	DW	0843CH
	DW	0FB4BH
	DW	FREAD
;
	DW	08424H
	DW	0FB33H
	DW	FSAVE
;
	DW	0840CH
	DW	0FB1BH
	DW	EXIT
;
	DB	0,0
;----
KADRTM: LXI	H,@TREK
	XRA	A
	CMP	M
	JZ	LOOP1
	DCR	M
	LXI	H,MTKM
	CALL	PRESS
	LXI	H,@TREK
	LXI	D,PRNTK
	CALL	KEYM
	JMP	KLIST
;
KADRTP: LXI	H,@TREK
	MVI	A,164
	CMP	M
	JZ	LOOP1
	INR	M
	LXI	H,MTKP
	CALL	PRESS
	LXI	H,@TREK
	LXI	D,PRNTK
	CALL	KEYP
KLIST:	CALL	PRNMP
	CALL	NCUR2
	CALL	CLSBOX
	LXI	D,PALIT2
	CALL	@PLTR
	HLT
	JMP	LIST
;
KADRPM: LDA	PKADR
	ANA	A
	JZ	LOOP1
	DCR	A
	STA	PKADR
	CALL	PRNPK
	LXI	H,MPKM
	CALL	PRESS
	LXI	H,PKADR
	LXI	D,PRNPK
	CALL	KEYM
	CALL	PRNMP
	JMP	LOOP1
;
KADRPP: LDA	PKADR
	CPI	164
	JZ	LOOP1
	INR	A
	STA	PKADR
	CALL	PRNPK
	LXI	H,MPKP
	CALL	PRESS
	LXI	H,PKADR
	LXI	D,PRNPK
	CALL	KEYP
	CALL	PRNMP
	JMP	LOOP1
;
KADRKM: LDA	KKADR
	ANA	A
	JZ	LOOP1
	DCR	A
	STA	KKADR
	CALL	PRNKK
	LXI	H,MKKM
	CALL	PRESS
	LXI	H,KKADR
	LXI	D,PRNKK
	CALL	KEYM
	CALL	PRNMP
	JMP	LOOP1
;
KADRKP: LDA	KKADR
	CPI	164
	JZ	LOOP1
	INR	A
	STA	KKADR
	CALL	PRNKK
	LXI	H,MKKP
	CALL	PRESS
	LXI	H,KKADR
	LXI	D,PRNKK
	CALL	KEYP
	CALL	PRNMP
	JMP	LOOP1
;
DISKA:	XRA	A
	STA	@DISK
	CALL	PRNDSK
	JMP	LOOP1
;
DISKB:	MVI	A,1
	STA	@DISK
	CALL	PRNDSK
	JMP	LOOP1
;
REVON:	MVI	A,0FFH
	STA	REVERS
	CALL	PRNREV
	JMP	LOOP1
;
REVOFF: XRA	A
	STA	REVERS
	CALL	PRNREV
	JMP	LOOP1
;
KLOAD:	LXI	H,MKLOAD
	CALL	PRESS
	CALL	NCUR2
	CALL	INNAME
	CALL	PURGE
	CALL	CLSBOX
	LXI	D,PALIT1
	CALL	@PLTR
	CALL	LPEN
	CALL	PUT
	LXI	D,MSAVE
	CALL	@WRITE
KLOADV: IN	1
	RLC
	RLC
	JNC	RETURN
	RLC
	JC	KLOADV
	CALL	PURGE
	JMP	RECPEN
;
KSAVE:	LXI	H,MKSAVE
	CALL	PRESS
	CALL	NCUR2
	CALL	INNAME
	CALL	PURGE
	LDA	@CLRS+1
	CPI	164
	JNZ	NOPER
	MVI	D,0CBH	; перенос в младшие плоскости
	MVI	B,08BH
KADRP1: MVI	E,080H
	MOV	C,E
KADRP2: LDAX	D
	STAX	B
	INR	C
	INR	E
	JNZ	KADRP2
	INR	B
	INR	D
	JNZ	KADRP1
	LXI	D,PALIT1
	CALL	@PLTR
NOPER:	CALL	GET
	CALL	SPEN
	JMP	RETURN
;
FREAD:	LXI	H,MFREAD
	CALL	PRESS
	CALL	NCUR2
	LXI	D,MSTOP
	CALL	@WRITE
	CALL	CLSBOX
	LXI	D,PALIT2
	CALL	@PLTRS
	HLT
	JMP	READ
;
FSAVE:	LXI	H,MFSAVE
	CALL	PRESS
	CALL	NCUR2
	CALL	CLSBOX
	LXI	D,MSAVE
	CALL	@WRITE
	LXI	D,PALIT1
	CALL	@PLTR
FSAVEV: CALL	VIEW
	IN	1
	RLC
	RLC
	JNC	RETURN
	RLC
	JC	FSAVEV
	CALL	PURGE
	LXI	D,MSTOP
	CALL	@WRITE
	JMP	RECORD
;
EXIT:	LXI	H,MEXIT
	CALL	PRESS
	LXI	H,MEXIT
	CALL	PRESS0
RST0:	EI
	LXI	D,PALIT0
	CALL	@PLTRS
	CALL	@CLS
	MVI	A,8
	STA	@TREK
	CALL	@GOTR
	CALL	@EXIT
	CALL	QUEST
	MVI	A,081H
	OUT	4
	MVI	C,020H
NUSER:	MVI	E,0
	CALL	00005H
	JMP	00000H
;
;==== ПЕРЕМЕЩЕНИЕ КУРСОРА ====
PERCUR: CALL	NCUR1
CURS:	CALL	INKEY
	HLT
	CALL	STRCUR
	LHLD	CURSYX
	LDA	KODKEY
	RLC
	CNC	CURSN
	RLC
	CNC	CURSP
	RLC
	CNC	CURSW
	RLC
	CNC	CURSL
	SHLD	CURSYX
	CALL	NCUR1
	LDA	KODKEY
	ANI	00000100B
	JNZ	CURS
	JMP	STRCUR
;----
CURSN:	MOV	C,A
	MVI	A,20
	CMP	L
	MOV	A,C
	RZ
	DCR	L
	DCR	L
	RET
;
CURSP:	MOV	C,A
	MVI	A,248
	CMP	H
	MOV	A,C
	RZ
	INR	H
	INR	H
	RET
;
CURSW:	MOV	C,A
	MVI	A,6
	CMP	L
	MOV	A,C
	RZ
	INR	L
	INR	L
	RET
;
CURSL:	MOV	C,A
	XRA	A
	CMP	H
	MOV	A,C
	RZ
	DCR	H
	DCR	H
	RET
;==== ОШИБКА ====
ERRORR: LXI	D,MERRORR
	CALL	@WRITE
	LDA	@TREK
	JMP	@DECA
;----
ERRORS: LXI	D,MERRORS
	CALL	@WRITE
	LDA	@TREK
	JMP	@DECA
;----
ERRORD: LXI	D,MERRORD
	CALL	@WRITE
	EI
	MVI	A,100
	CALL	PAUSE
;==== РАЗВРАТ ====
RETURN: MVI	A,130
	OUT	4
	LXI	SP,START
	EI
	CALL	STRCUR
	CALL	PURGE
	CALL	PRNMNU
	JMP	LOOP1
;----
; подчистить табло
PURGE:	LXI	D,02000H
	LXI	B,-06000H
	MVI	H,08EH
PURGE1: MVI	L,058H
	MVI	A,078H
PURGE2: MVI	M,0
	DAD	D
	MVI	M,0FFH
	DAD	D
	MVI	M,0
	DAD	D
	MVI	M,0FFH
	DAD	B
	INR	L
	CMP	L
	JNZ	PURGE2
	INR	H
	MVI	A,09EH
	CMP	H
	JNZ	PURGE1
	RET
;==== ОЧИСТКА ЭКРАНЧИКА ====
CLSBOX: LXI	D,02000H
	LXI	B,-06000H
	MVI	H,08BH
CLSB1:	XRA	A
	MVI	L,080H
CLSB2:	MOV	M,A
	DAD	D
	MOV	M,A
	DAD	D
	MOV	M,A
	DAD	D
	MOV	M,A
	DAD	B
	INR	L
	JNZ	CLSB2
	INR	H
	MVI	A,09FH
	CMP	H
	JNZ	CLSB1
	RET
;==== Нажать ====
PRESS:
; HL - адрес с данными
	CALL	PRESS1
	LXI	H,CURSYX
	INR	M
	CALL	NCUR1
	MVI	A,1
	OUT	1
	MVI	A,10
	CALL	PAUSE
	CALL	STRCUR
	LXI	H,CURSYX
	DCR	M
	RET
;---- Нажать ----
; HL - адрес с данными
PRESS1: CALL	DATPRS
	LXI	H,04000H
	DAD	D
	CALL	PRS10
	XCHG
;
PRS10:	MVI	C,11
PRS11:	MOV	A,M
	ANI	11111011B
	MOV	M,A
	INR	L
	DCR	C
	JNZ	PRS11
;
	MOV	A,M
	ANI	11111001B
	MOV	M,A
	INR	L
	MOV	A,M
	ANI	11111010B
	MOV	M,A
	INR	L
	MOV	A,M
	ANI	11111000B
	MOV	M,A
;
	MOV	C,B
PRS12:	INR	H
	MVI	M,0
	DCR	L
	MOV	A,M
	ANI	10101010B
	MOV	M,A
	DCR	L
	MOV	A,M
	ANI	01010101B
	MOV	M,A
	INR	L
	INR	L
	DCR	C
	JNZ	PRS12
;
	INR	H
	MOV	A,M
	ANI	00011111B
	MOV	M,A
	DCR	L
	MOV	A,M
	ANI	10111111B
	MOV	M,A
	DCR	L
	MOV	A,M
	ANI	01111111B
	MOV	M,A
	RET
;---- Отпустить ----
; HL - адрес с данными
PRESS0: CALL	DATPRS
	LXI	H,04000H
	DAD	D
	CALL	PRS00
	XCHG
;
PRS00:	MVI	C,11
PRS01:	MOV	A,M
	ORI	00000100B
	MOV	M,A
	INR	L
	DCR	C
	JNZ	PRS01
;
	MOV	A,M
	ORI	00000110B
	MOV	M,A
	INR	L
	MOV	A,M
	ORI	00000101B
	MOV	M,A
	INR	L
	MOV	A,M
	ORI	00000111B
	MOV	M,A
;
	MOV	C,B
PRS02:	INR	H
	MVI	M,0FFH
	DCR	L
	MOV	A,M
	ORI	01010101B
	MOV	M,A
	DCR	L
	MOV	A,M
	ORI	10101010B
	MOV	M,A
	INR	L
	INR	L
	DCR	C
	JNZ	PRS02
;
	INR	H
	MOV	A,M
	ORI	11100000B
	MOV	M,A
	DCR	L
	MOV	A,M
	ORI	01000000B
	MOV	M,A
	DCR	L
	MOV	A,M
	ORI	10000000B
	MOV	M,A
	RET
;----
DATPRS: MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	MOV	B,M
	RET
;---- Постоянное нажатие ----
; HL - изменяемая ячейка
; DE - отображающая подпрограмма
; Минус
KEYM:	MVI	A,035H		; DCR M
	STA	KEY4
	XRA	A
	JMP	KEY0
; Плюс
KEYP:	MVI	A,034H		; INR M
	STA	KEY4
	MVI	A,164
;----
KEY0:	STA	KEY3+1
	SHLD	KEY2+1
	XCHG
	SHLD	KEY5+1
KEY1:	CALL	NCUR1
	HLT
	HLT
	CALL	INKEY
	CALL	STRCUR
	LDA	KODKEY
	CPI	11111011B
	RNZ
KEY2:	LXI	H,0
KEY3:	MVI	A,0
	CMP	M
	RZ
KEY4:	NOP		; + / -
KEY5:	CALL	0
	JMP	KEY1
;==== МЕНЮ ====
PRNMNU: LXI	H,MFREAD
	CALL	PRESS0
	LXI	H,MFSAVE
	CALL	PRESS0
	LXI	H,MKLOAD
	CALL	PRESS0
	LXI	H,MKSAVE
	CALL	PRESS0
	LXI	H,MEXIT
	JMP	PRESS0
;==== + - ====
PRNMP:	LXI	H,MTKM
	CALL	PRESS0
	LXI	H,MTKP
	CALL	PRESS0
;
	LXI	H,MPKM
	CALL	PRESS0
	LXI	H,MPKP
	CALL	PRESS0
;
	LXI	H,MKKM
	CALL	PRESS0
	LXI	H,MKKP
	JMP	PRESS0
;==== КОЛ-ВО ====
PRNTK:	LXI	D,MTKADR
	CALL	@WRITE
	LDA	@TREK
	JMP	@DECA
;
PRNPK:	LXI	D,MPKADR
	CALL	@WRITE
	LDA	PKADR
	JMP	@DECA
;
PRNKK:	LXI	D,MKKADR
	CALL	@WRITE
	LDA	KKADR
	JMP	@DECA
;==== АВТОРЕВЕРС ====
PRNREV: LDA	REVERS
	ANA	A
	LXI	H,MRON
	LXI	D,MROFF
	JZ	PRNPRS
	XCHG
	JMP	PRNPRS
;==== ДИСКИ A/B ====
PRNDSK: LDA	@DISK
	ANA	A
	LXI	H,MDISKB
	LXI	D,MDISKA
	JZ	PRNPRS
	XCHG
;----
PRNPRS: PUSH	D
		CALL	PRESS0
	POP	H
	JMP	PRESS
;----
MTKM:	DW	0A1DDH
	DB	1
MTKP:	DW	0A7DDH
	DB	1
MPKM:	DW	0A1B5H
	DB	1
MPKP:	DW	0A7B5H
	DB	1
MKKM:	DW	0A18DH
	DB	1
MKKP:	DW	0A78DH
	DB	1
MDISKA: DW	0A16DH
	DB	1
MDISKB: DW	0A76DH
	DB	1
MRON:	DW	0A145H
	DB	3
MROFF:	DW	0A645H
	DB	4
MKLOAD: DW	0A025H
	DB	14
MKSAVE: DW	0A00DH
	DB	14
MFREAD: DW	0B03DH
	DB	14
MFSAVE: DW	0B025H
	DB	14
MEXIT:	DW	0B00DH
	DB	14
CURSYX: DB	048H,070H
;
PKADR:	DB	0
KKADR:	DB	164
REVERS: DB	0FFH
;
MTKADR: DB	01BH,3,4,0
MPKADR: DB	01BH,8,4,0
MKKADR: DB	01BH,13,4,0
;
MSAVE:	DB	01BH,18,16,'Запись - "СС"'
	DB	01BH,19,16,'Отмена - "УС"',0
;
MSTOP:	DB	01BH,18,17,'Стоп - "УС"',0
;
MERRORR:
	DB	01BH,18,15,'Ошибка  чтения'
	DB	01BH,19,17,  'кадр - ',0
MERRORS:
	DB	01BH,18,15,'Ошибка  записи'
	DB	01BH,19,17,  'кадр - ',0
MERRORD:
	DB	01BH,18,15,'Диск не готов',0
;
	END
